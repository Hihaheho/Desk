%start ExprC
%title "Minimalist syntax for Desk-lang"
%comment ""

%scanner String {
    %auto_ws_off
    %auto_newline_off
}
%scanner RawString1 {
    %auto_ws_off
    %auto_newline_off
}
%scanner RawString2 {
    %auto_ws_off
    %auto_newline_off
}
%scanner BlockComment {
    %auto_ws_off
    %auto_newline_off
}
%scanner InlineComment {
    %auto_ws_off
}
%scanner Uuid {}
%scanner Ident {}

%%

ExprC: { Comment } Expr;
Expr: ExprBegin^ ExprC ExprEnd^
    | Hole
    | Do
    | Cast
    | Literal
    | Let
    | Perform
    | Continue
    | Handle
    | Product
    | Vector
    | Map
    | Attributed
    | Match
    | Function
    | Apply
    | Reference
    | Forall
    | Exists
    | Labeled
    | NewType
    | Card
    | Brand
    ;
    Do: DoKey^ ExprC StmtEnd^ ExprC;
    Cast: TypeBegin^ Ty TypeEnd^ ExprC;
    Literal: Rational | Integer | Real | String | RawString;
        // Rational must be before Integer for higher priority.
        Rational: "\-?\p{Nd}+\s*/\s*\p{Nd}+";
        // Real must be before Integer for higher priority.
        Real: /\-?\p{Nd}+\.\p{Nd}+/;
        Integer: Hex | Oct | Bin | Dec;
            // Dec must be the last for lower priority.
            Hex: /0x[0-9a-fA-F]+/;
            Oct: /0o[0-7]+/;
            Bin: /0b[01]+/;
            Dec: /\-?\p{Nd}+/;
        String: StringDelimiter^ %push(String) { Escaped | Characters | Newlines } StringDelimiter^ %pop();
            Escaped: <String> /\\[nt"\\]/;
            Characters: <String> /[^\\"]+/;
            Newlines: <String> /\\n+/;
        RawString: RawString1 | RawString2;
        RawString1: '«'^ %push(RawString1) [ RawCharacters1 ] %pop() '»'^;
            RawCharacters1: <RawString1> /[^»]+/;
        RawString2: '‹'^ %push(RawString2) [ RawCharacters2 ] %pop() '›'^;
            RawCharacters2: <RawString2> /[^›]+/;
    Let: LetSym^ ExprC StmtEnd^ ExprC;
    Perform: PerformSym^ ExprC EArrow^ Ty;
    Continue: ContinueSym^ ExprC EArrow^ Ty;
    Handle: HandleKey^ ExprC Begin^ { Handler [ Comma^ ] } End^;
        Handler: { Comment } Ty EArrow^ Ty Arrow^ ExprC;
    Product: ProductSym^ TypeBegin^ { ExprC [ Comma^ ] } TypeEnd^;
    Vector: VecBegin^ { ExprC [ Comma^ ] } VecEnd^;
    Map: MapBegin^ { ExprC Arrow^ ExprC [ Comma^ ] } MapEnd^;
    Attributed: Attribute ExprC;
    Match: MatchKey^ ExprC Begin^ { Case [ Comma^ ] } End^;
        Case: { Comment } Ty Arrow^ ExprC;
    Function: FunctionSym^ Ty FArrow^ ExprC;
    // We need `ApplySym` to make it deterministic on such a token: `*`.
    Apply: ApplySym^ [ LinkName ] Ty ( Param | Params );
        Param: ExprC;
        Params: ParamsBegin^ { ExprC [ Comma^ ] } ParamsEnd^;
    Reference: ReferenceSym^ [ LinkName ] Ty;
    Labeled: Label ExprC;
    Forall: ForallKey^ BoundedVariable ExprC;
    Exists: ExistsKey^ BoundedVariable ExprC;
    NewType: TyKey^ Ident Ty StmtEnd^ ExprC;
    Card: CardKey^ Uuid ExprC StmtEnd^ ExprC;
    Brand: BrandKey^ Ident StmtEnd^ ExprC;

Ty: ExprBegin^ Ty ExprEnd^
  | Infer
  | RealKey
  | RationalKey
  | IntegerKey
  | StringKey
  | Effectful
  | ProductTy
  | Sum
  | VecTy
  | MapTy
  | FunctionTy
  | LabeledTy
  | AttributedTy
  | Variable
  | LetTy
  | ForallTy
  | ExistsTy
  ;
    Effectful: PerformSym^ EffectExpr Ty;
        EffectExpr: Effects | AddEffects | SubEffects| ApplyEffects;
        Effects: MapBegin^ { Effect [ Comma^ ] } MapEnd^;
        AddEffects: SumSym^ TypeBegin^ { EffectExpr [ Comma^ ] } TypeEnd^;
        SubEffects: Minus^ TypeBegin^ EffectExpr [ Comma^ ] EffectExpr TypeEnd^;
        ApplyEffects: ApplySym^ Ty ParamsBegin^ { Ty [ Comma^ ] } ParamsEnd^;
    ProductTy: ProductSym^ TypeBegin^ { Ty [ Comma^ ] } TypeEnd^;
    Sum: SumSym^ TypeBegin^ { Ty [ Comma^ ] } TypeEnd^;
    VecTy: VecBegin^ Ty VecEnd^;
    MapTy: MapBegin^ Ty Arrow^ Ty MapEnd^;
    FunctionTy: FunctionSym^ Ty FArrow^ Ty;
    LabeledTy: Label Ty;
    AttributedTy: Attribute Ty;
    Variable: Ident;
    LetTy: LetSym^ Ident Ty StmtEnd^ Ty;
    ForallTy: ForallKey^ BoundedVariable Ty;
    ExistsTy: ExistsKey^ BoundedVariable Ty;

// Utilities
// TODO: Add this to the beginning of all nodes.
Comment: BlockComment
       | InlineComment;
    // This must come before ParamsEnd for higher priority.
    BlockComment: CommentBegin^ %push(BlockComment) { BlockCommentContent } { ParamsEnd } CommentEnd ^ %pop();
    InlineComment: CommentPrefix^ %push(InlineComment) CommentCharacters %pop();
    CommentEnd: <BlockComment> ')~';
    BlockCommentContent: <BlockComment> /[^\)]+(\)[^~\)])?/;
    CommentCharacters: <InlineComment> /.*/;
Attribute: AttributeSym^ ExprC;
Label: LabelSym^ Ident;
Ident: IdentRaw
     | IdentWrapped;
    IdentWrapped: IdentDelimiter^ %push(Ident) IdentPart { IdentPart } IdentDelimiter^ %pop();
    IdentRaw: /[^!-@\[-`\{-~\s]+/;
    IdentPart: <Ident> /([^`\s]|\\`)+/;
    IdentDelimiter: <INITIAL, Ident> '`';
Uuid: %push(Uuid) UuidText %pop();
    UuidText: <Uuid> /[0-9a-fA-F]{8}-?[0-9a-fA-F]{4}-?[0-9a-fA-F]{4}-?[0-9a-fA-F]{4}-?[0-9a-fA-F]{12}/;
Effect: Ty EArrow^ Ty;
LinkName: CardKey^ Uuid | VersionKey^ Uuid;
// Tail comma is allowed, and this is important for writing readable code.
BoundedVariable: Ident [ TyAnno^ Ty ] [ Comma^ ];

// Composite tokens
// These must be listed first for higher priority.
Arrow: '=>';
FArrow: '->';
EArrow: '~>';
ContinueSym: '!<~';
CommentBegin: '~(';
Begin: "'\{";
End: "\}'";
ExprBegin: "'\(";
ExprEnd: "\)'";

// Tokens
// ".", "=" is not used.
CommentPrefix: '~';
Comma: ',';
StmtEnd: ';';
TyAnno: ':';
MapBegin: '{';
MapEnd: '}';
Hole: '?';
VecBegin: '[';
VecEnd: ']';
TypeBegin: '<';
TypeEnd: '>';
ParamsBegin: '(';
ParamsEnd: <INITIAL, BlockComment>')';
LabelSym: '@';
Infer: '_';
PerformSym: '!';
ReferenceSym: '&';
ApplySym: '^';
ProductSym: '*';
SumSym: '+';
Minus: '-';
LetSym: '$';
AttributeSym: '#';
// \u{5c} is '\'.
FunctionSym: /\u{5c}/;
StringDelimiter: <INITIAL, String>'"';

// Keywords
DoKey: "'do";
TyKey: "'type";
ForallKey: "'forall";
ExistsKey: "'exists";
CardKey: "'card";
BrandKey: "'brand";
StringKey: "'string";
RealKey: "'real";
RationalKey: "'rational";
IntegerKey: "'integer";
HandleKey: "'handle";
MatchKey: "'match";
VersionKey: "'version";
